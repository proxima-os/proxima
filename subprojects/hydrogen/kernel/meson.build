linker_script = meson.current_source_dir() / 'kernel.lds'

stripped_vdso = custom_target(
    'stripped-vdso',
    command: [strip, '@INPUT@', '-o@OUTPUT@'],
    input: vdso,
    output: 'stripped-vdso.so',
)
vdso_object = custom_target(
    'vdso-object',
    command: [objcopy, '@INPUT@', '@OUTPUT@', '-Ibinary', '-Oelf64-x86-64', '--rename-section=.data=.vdso'],
    input: stripped_vdso,
    output: 'vdso.o',
)

executable(
    'hydrogen',
    'src/cpu/cpu.c',
    'src/cpu/exc.c',
    'src/cpu/gdt.c',
    'src/cpu/gdt.S',
    'src/cpu/idt.c',
    'src/cpu/idt.S',
    'src/cpu/lapic.c',
    'src/cpu/lapic.S',
    'src/cpu/tss.c',
    'src/cpu/xsave.c',
    'src/drv/acpi.c',
    'src/drv/hpet.c',
    'src/drv/pci.c',
    'src/drv/pic.c',
    'src/init/main.c',
    'src/init/main.S',
    'src/mem/heap.c',
    'src/mem/kvmm.c',
    'src/mem/pmap.c',
    'src/mem/pmm.c',
    'src/mem/vheap.c',
    'src/mem/vmm.c',
    'src/sched/mutex.c',
    'src/sched/proc.c',
    'src/sched/sched.c',
    'src/sched/sched.S',
    'src/sched/sema.c',
    'src/sys/syscall.c',
    'src/sys/syscall.S',
    'src/sys/vdso.c',
    'src/util/idmap.c',
    'src/util/list.c',
    'src/util/panic.c',
    'src/util/print.c',
    'src/util/spinlock.c',
    'src/util/string.c',
    'src/util/time.c',
    'src/util/xarray.c',
    'uacpi/source/default_handlers.c',
    'uacpi/source/event.c',
    'uacpi/source/interpreter.c',
    'uacpi/source/io.c',
    'uacpi/source/mutex.c',
    'uacpi/source/namespace.c',
    'uacpi/source/notify.c',
    'uacpi/source/opcodes.c',
    'uacpi/source/opregion.c',
    'uacpi/source/osi.c',
    'uacpi/source/registers.c',
    'uacpi/source/resources.c',
    'uacpi/source/shareable.c',
    'uacpi/source/sleep.c',
    'uacpi/source/stdlib.c',
    'uacpi/source/tables.c',
    'uacpi/source/types.c',
    'uacpi/source/uacpi.c',
    'uacpi/source/utilities.c',
    c_args: [
        '-ffreestanding',
        '-fno-pie',
        '-fno-stack-check',
        '-fno-stack-protector',
        '-mcmodel=kernel',
        '-mgeneral-regs-only',
        '-mno-red-zone',
    ],
    implicit_include_directories: false,
    include_directories: [api_inc, priv_inc],
    install: true,
    install_dir: '/boot',
    install_mode: 'rw-r--r--',
    install_tag: 'runtime',
    link_args: ['-nostdlib', '-static', '-T' + linker_script],
    link_depends: linker_script,
    objects: vdso_object,
    sources: lib_sources,
)
